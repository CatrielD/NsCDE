WindowTitle {Subpanel Settings}
WindowSize 370 270
Colorset 22

Init
Begin
   Set $SubpanelName = (GetScriptArgument 1)
   Set $SubpanelConfNameCmd = {echo '} $SubpanelName {' | sed 's/^NsCDE-//g'}
   Set $SubpanelConfName = (GetOutput $SubpanelConfNameCmd 1 -1)
   Set $SubpanelDisplayNameVar = $SubpanelName {-Name}
   Set $SubpanelWidthVar = $SubpanelName {-Width}
   Set $SubpanelRowsVar = $SubpanelName {-Rows}

   Set $SubpanelConfigExistanceCmd = {[[ -r "$FVWM_USERDIR/} $SubpanelName {.conf" ]] && echo 1 || echo 0}
   Set $SubpanelConfigExistance = (GetOutput $SubpanelConfigExistanceCmd 1 1)

   If $SubpanelConfigExistance == 0 Then
   Begin
      Set $ConfDirPath = {$NSCDE_ROOT/config}
      Set $NeedInitLocalCopy = 1
   End
   Else
   Begin
      Set $ConfDirPath = {$FVWM_USERDIR}
      Set $NeedInitLocalCopy = 0
   End

   Set $SubpanelDataCmd = {$NSCDE_ROOT/libexec/subpanelsettings -C "} $ConfDirPath {/} $SubpanelName {.conf" -N } $SubpanelName { -g}
   Set $SubpanelDisplayName = (GetOutput $SubpanelDataCmd 1 -1)
   Set $SubpanelWidth = (GetOutput $SubpanelDataCmd 2 -1)
   Set $SubpanelRows = (GetOutput $SubpanelDataCmd 3 -1)
   Set $WindowTitlePrint = {Subpanel } $SubpanelDisplayName { Settings}
   ChangeWindowTitle $WindowTitlePrint
   ChangeTitle 1 $WindowTitlePrint
   ChangeTitle 5 $SubpanelDisplayName
   ChangeTitle 8 $SubpanelWidth
   ChangeTitle 11 $SubpanelRows

   Set $SubpanelSysDataCmd = {$NSCDE_ROOT/libexec/subpanelsettings -C "$NSCDE_ROOT/config/} $SubpanelName {.conf" -N } $SubpanelName { -g}
   Set $SubpanelSysDisplayName = (GetOutput $SubpanelSysDataCmd 1 -1)
   Set $SubpanelSysWidth = (GetOutput $SubpanelSysDataCmd 2 -1)
   Set $SubpanelSysRows = (GetOutput $SubpanelSysDataCmd 3 -1)

   # Ta daaa ...
   Do {SendToModule FrontPanel PressButton } $SubpanelName

   # Cursor on the end of input form please
   ChangeMinValue 5 9999
   ChangeMinValue 8 9999
   ChangeMinValue 11 9999

   # Nasty workaround because ForeColor of the TextField
   # widget is not applied and global value is used anyway.
   # How it works: if wanted Colorset is applied with
   # ChangeColorset to (any?) other widget after initialization
   # of the script, colorset foreground color will be properly
   # applied to TextField widget too. Even if fake widget is
   # hidden, this workaround still works as it should. 
   # We define hidden PushButton widhet on the end of the script
   # and send string "workaround" to routine 1 of the TextField
   # widget ("1 : " can be attached to any widget in fact ...).
   # This routine than changes the colorset of the hidden
   # PushButton widget, and this then triggers change in buggy
   # TextField to be applied.
   # This bug can be seen when default colorset for the script has
   # different foreground color than colorset applied to the
   # TextField widget. Another possible workaround is to define
   # globally back/fore/sh/hi colors for the script instead of
   # (or after definition) of a colorset, but this is unflexible,
   # since script background cannot be changed dinamically or
   # from outside of the script as colorset functionality allows.
   Do {Schedule 50 SendToModule SubpanelSettings SendString 17 1 workaround}
   Do {Schedule 250 SendToModule SubpanelSettings SendString 17 1 workaround}
   Do {Schedule 350 SendToModule SubpanelSettings SendString 17 1 workaround}
End

Widget 1
   Property
   Position 6 10
   Size 320 20
   Type ItemDraw
   Flags NoReliefString Left
   Title {Subpanel Settings: }
   Main
      Case message of
      SingleClic :
      Begin
      End
End

Widget 2
   Property
   Size 20 0
   Position 302 6
   Flags NoReliefString
   Type PushButton
   Title {Reset}
   Main
      Case message of
      SingleClic :
      Begin
         If $NeedInitLocalCopy == 0 Then
         Begin
            Set $ResetCfCmd = {[[ -r "$FVWM_USERDIR/} $SubpanelName {.conf" ]] && rm -f "$FVWM_USERDIR/} $SubpanelName {.conf"}
            Set $ResetCf = (GetOutput $ResetCfCmd 1 -1)
            Set $ConfDirPath = {$NSCDE_ROOT/config}
            Set $NeedInitLocalCopy = 1

            Set $SubpanelDataCmd = {$NSCDE_ROOT/libexec/subpanelsettings -C "} $ConfDirPath {/} $SubpanelName {.conf" -N } $SubpanelName { -g}
            Set $SubpanelDisplayName = (GetOutput $SubpanelDataCmd 1 -1)
            Set $SubpanelWidth = (GetOutput $SubpanelDataCmd 2 -1)
            Set $SubpanelRows = (GetOutput $SubpanelDataCmd 3 -1)
            Set $WindowTitlePrint = {Subpanel } $SubpanelDisplayName { Settings}
            ChangeWindowTitle $WindowTitlePrint
            ChangeTitle 1 $WindowTitlePrint
            ChangeTitle 5 $SubpanelDisplayName
            ChangeTitle 8 $SubpanelWidth
            ChangeTitle 11 $SubpanelRows

            Set $ChangeDetectString = $SubpanelDisplayName $SubpanelWidth $SubpanelRows

            Do {f_ReadCfg } $SubpaneConfName
            Do {KillModule FvwmButtons } $SubpanelName
            Do {Schedule 800 SendToModule FrontPanel PressButton } $SubpanelName
         End
      End
End

Widget 3
   Property
   Size 358 168
   Position 6 42
   Type Rectangle
End

Widget 4
   Property
   Position 14 54
   Size 30 20
   Type ItemDraw
   Flags NoReliefString Left
   Title {Subpanel Name: }
   Main
      Case message of
      SingleClic :
      Begin
      End
End

Widget 5
   Property
   Position 14 80
   Size 256 20
   Type TextField
   Flags NoReliefString Left
   Title {}
   Colorset 20
   Main
      Case message of
      SingleClic :
      Begin
      End
End

Widget 6
   Property
   Size 20 0
   Position 284 78
   Flags NoReliefString
   Type PushButton
   Title {Default}
   Main
      Case message of
      SingleClic :
      Begin
         ChangeTitle 5 $SubpanelSysDisplayName
      End
End

Widget 7
   Property
   Position 16 124
   Size 30 20
   Type ItemDraw
   Flags NoReliefString Left
   Title {Subpanel Title List Width: }
   Main
      Case message of
      SingleClic :
      Begin
      End
End

Widget 8
   Property
   Position 222 120
   Size 48 20
   Type TextField
   Flags NoReliefString Left
   Title {}
   Colorset 20
   Main
      Case message of
      SingleClic :
      Begin
      End
End

Widget 9
   Property
   Size 20 0
   Position 284 118
   Flags NoReliefString
   Type PushButton
   Title {Default}
   Main
      Case message of
      SingleClic :
      Begin
         ChangeTitle 8 $SubpanelSysWidth
      End
End

Widget 10
   Property
   Position 16 164
   Size 30 20
   Type ItemDraw
   Flags NoReliefString Left
   Title {Maximum Subpanel Items: }
   Main
      Case message of
      SingleClic :
      Begin
      End
End

Widget 11
   Property
   Position 222 160
   Size 48 20
   Type TextField
   Flags NoReliefString Left
   Title {}
   Colorset 20
   Main
      Case message of
      SingleClic :
      Begin
      End
End

Widget 12
   Property
   Size 20 0
   Position 284 158
   Flags NoReliefString
   Type PushButton
   Title {Default}
   Main
      Case message of
      SingleClic :
      Begin
         ChangeTitle 11 $SubpanelSysRows
      End
End

Widget 13
   Property
   Size 360 0
   Position 4 222
   Type Rectangle
End

Widget 14
   Property
   Size 60 0
   Position 10 232
   Flags NoReliefString
   Type PushButton
   Title {OK}
   Main
      Case message of
      SingleClic :
      Begin
         Set $ChangeFields = (GetTitle 5) (GetTitle 8) (GetTitle 11)
         If $ChangeFields == $ChangeDetectString Then
         Begin
            Quit
         End

         If $NeedInitLocalCopy == 1 Then
         Begin
            Set $ForkConfigCmd = {cp -f "} $ConfDirPath {/} $SubpanelName {.conf" "$FVWM_USERDIR/} $SubpanelName {.conf"}
            Set $ForkConfig = (GetOutput $ForkConfigCmd 1 -1)
            Set $NeedInitLocalCopy = 0
            Set $ConfDirPath = {$FVWM_USERDIR}
         End
         Set $NewSubpanelName = (GetTitle 5)
         Set $NewSubpanelWidth = (GetTitle 8)
         Set $NewSubpanelRows = (GetTitle 11)
         Set $WriteNewSubpanelConfCmd = {$NSCDE_ROOT/libexec/subpanelsettings -C "} $ConfDirPath {/} $SubpanelName {.conf" -N '} $SubpanelName {' -w '} $NewSubpanelName {,} $NewSubpanelWidth {,} $NewSubpanelRows {'}
         Set $WriteNewSubpanelConf = (GetOutput $WriteNewSubpanelConfCmd 1 -1)
         Do {f_ReadCfg } $SubpanelConfName
         Do {KillModule FvwmButtons } $SubpanelName
         Do {Schedule 800 SendToModule FrontPanel PressButton } $SubpanelName
         Quit
      End
End

Widget 15
   Property
   Size 60 0
   Position 152 232
   Flags NoReliefString
   Type PushButton
   Title {Close}
   Main
      Case message of
      SingleClic :
      Begin
         Quit
      End
End

Widget 16
   Property
   Size 60 0
   Position 300 232
   Flags NoReliefString
   Type PushButton
   Title {Help}
   Main
      Case message of
      SingleClic :
      Begin
         Do {f_DisplayURL "Subpanel Settings" $[NSCDE_ROOT]/share/doc/html/NsCDE-SubpanelSettings.html}
      End
End

# Nasty workaround because ForeColor of the TextField
# widget is not applied and global value is used anyway.
# See end of the Init procedure for explanation.
Widget 17
   Property
   Size 40 10
   Position 10 10
   Type PushButton
   Flags NoReliefString NoFocus Hidden
   Title {XXXXXXXXXXXXXXXXXXXXXXXXXXX}
   Main
      Case message of
      SingleClic :
      Begin
      End
      1 :
      Begin
         If (LastString) == {workaround} Then
         Begin
            ChangeColorset 17 20
            ChangeColorset 5 20
            ChangeColorset 8 20
            ChangeColorset 11 20
         End
      End
End

