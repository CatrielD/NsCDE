#
# This file is a part of the NsCDE - Not so Common Desktop Environment
# Author: Hegel3DReloaded
# Licence: GPLv3
#

WindowTitle {Style Manager - Powersave}
WindowSize 386 420
Colorset 22

Init
Begin
   Set $genfile = (GetOutput {sh -c "echo $FVWM_USERDIR/Xset.conf"} -1 1)
   Set $FILE = $genfile
   Set $Wrong = 0

   Set $pos=0
   Set $finish=1
   While $finish=={1} Do
   Begin
      Set $pos=(Add $pos 1)
      Set $tmp=(GetOutput {xset -q} $pos 1)
      If $tmp=={Standby:} Then
         Set $finish=0
      If $pos=={50} Then
         Set $finish=0
   End

   Set $pos2=(Add $pos 1)

   If $pos == {50} Then
   Begin
      Do {None ("Power Style Manager Error")}
          { FvwmForm ErrMsgForm TITLE="Power Style Manager Error"}
          { TEXT="Error: Incompatible xset(1) tool or no DPMS functionality detected."}
      Quit
   End

   ChangeValue 4 (GetOutput {xset -q} $pos 2)
   ChangeValue 7 (GetOutput {xset -q} $pos 6)
   ChangeValue 9 (GetOutput {xset -q} $pos 4)

   Set $Enabled = (GetOutput {xset -q} $pos2 3)

   If $Enabled == {Disabled} Then
   Begin
      ChangeValue 6 0
      HideWidget 4
      HideWidget 5
      HideWidget 7
      HideWidget 8
      HideWidget 9
      HideWidget 10
   End

   # XScreenSaver Integration / sync in DPMS part
   Set $XSCREENSAVERCONF = (GetOutput {echo $HOME/.xscreensaver} -1 1)

   # Key bindings
   Key Escape A 3 1 {KeyClose}
   Key Q C 3 1 {KeyClose}
End
 
Widget 1
   Property
   Size 72 0
   Position 10 378
   Flags NoReliefString
   Type PushButton
   Title {Apply}
   Main
      Case message of
      SingleClic :
      Begin
         If (GetValue 4) > (GetValue 9) Then
         Begin
            Do {FvwmForm ErrMsgForm TITLE="Powersave Style Manager" TEXT="Standby time cannot be bigger than Suspend time."}
            Set $Wrong = 1
         End
         Else
         Begin
            Set $Wrong = 0
         End

         If (GetValue 4) > (GetValue 7) Then
         Begin
            Do {FvwmForm ErrMsgForm TITLE="Powersave Style Manager" TEXT="Standby time cannot be bigger than Off time."}
            Set $Wrong = 1
         End
         Else
         Begin
            Set $Wrong = 0
         End

         If (GetValue 9) > (GetValue 7) Then
         Begin
            Do {FvwmForm ErrMsgForm TITLE="Powersave Style Manager" TEXT="Suspend time cannot be bigger than Off time."}
            Set $Wrong = 1
         End
         Else
         Begin
            Set $Wrong = 0
         End

         If (GetValue 9) < (GetValue 4) Then
         Begin
            Do {FvwmForm ErrMsgForm TITLE="Powersave Style Manager" TEXT="Suspend time cannot be smaller than Standby time."}
            Set $Wrong = 1
         End
         Else
         Begin
            Set $Wrong = 0
         End

         If (GetBack 4) == {ff0000} Then
         Begin
            Set $Wrong = 1
         End

         If (GetBack 7) == {ff0000} Then
         Begin
            Set $Wrong = 1
         End

         If (GetBack 9)=={ff0000} Then
         Begin
            Set $Wrong = 1
         End

         If $Wrong == 0 Then
         Begin
            If (GetValue 6) == 1 Then
            Begin
               Do {Exec xset +dpms dpms } (GetValue 4) { } (GetValue 9) { } (GetValue 7)
            End
            Else
            Begin
               Do {Exec xset -dpms }
            End
         End
      End
End

Widget 2
   Property
   Size 72 0
   Position 108 378
   Flags NoReliefString
   Type PushButton
   Title {Save}
   Main
      Case message of
      SingleClic :
      Begin
         If (GetValue 4) > (GetValue 9) Then
         Begin
            Do {FvwmForm ErrMsgForm TITLE="Powersave Style Manager" TEXT="Standby time cannot be bigger than Suspend time."}
            Set $Wrong = 1
         End
         Else
         Begin
            Set $Wrong = 0
         End

         If (GetValue 4) > (GetValue 7) Then
         Begin
            Do {FvwmForm ErrMsgForm TITLE="Powersave Style Manager" TEXT="Standby time cannot be bigger than Off time."}
            Set $Wrong = 1
         End
         Else
         Begin
            Set $Wrong = 0
         End

         If (GetValue 9) > (GetValue 7) Then
         Begin
            Do {FvwmForm ErrMsgForm TITLE="Powersave Style Manager" TEXT="Suspend time cannot be bigger than Off time."}
            Set $Wrong = 1
         End
         Else
         Begin
            Set $Wrong = 0
         End

         If (GetValue 9) < (GetValue 4) Then
         Begin
            Do {FvwmForm ErrMsgForm TITLE="Powersave Style Manager" TEXT="Suspend time cannot be smaller than Standby time."}
            Set $Wrong = 1
         End
         Else
         Begin
            Set $Wrong = 0
         End

         If $Wrong == 0 Then
         Begin
            # Workaround for FvwmScript WriteToFile which truncates existing file if it
            # doesn't find script's signature in it.
            Set $CheckEntryCmd = {egrep -q '^#PowerSaveMgr,[1-9]*' "} $FILE {"; echo $?}
            Set $CheckEntry = (GetOutput $CheckEntryCmd 1 -1)

            If $CheckEntry <> 0 Then
            Begin
               Set $PrepareFileCmd = {(echo '#PowerSaveMgr,00000'; echo '#end') >> "} $FILE {"}
               Set $PrepareFile = (GetOutput $PrepareFileCmd 1 -1)
            End

            If (GetValue 6) == 1 Then
            Begin
               Do {Exec xset dpms } (GetValue 4) { } (GetValue 9) { } (GetValue 7)
               Do {Exec xset +dpms}
               WriteToFile $FILE {xset +dpms}
               If $XSCREENSAVERCONF <> {} Then
               Begin
                  Set $EditXscOneCmd= {$NSCDE_ROOT/bin/ised -c 's/dpmsEnabled:.*/dpmsEnabled:	True/g' -f "} $XSCREENSAVERCONF {"}
                  Set $EditXscOne = (GetOutput $EditXscOneCmd 1 -1)
               End
            End
            Else
            Begin
               Do {Exec xset -dpms}
               WriteToFile $FILE {xset -dpms}
               If $XSCREENSAVERCONF <> {} Then
               Begin
                  Do {Exec $NSCDE_ROOT/bin/ised -c 's/dpmsEnabled:.*/dpmsEnabled:	False/g' -f "} $XSCREENSAVERCONF {"}
               End
            End

            If (GetValue 6) == 1 Then
            Begin
               Do {Exec xset dpms } (GetValue 4) { } (GetValue 9) { } (GetValue 7)
               WriteToFile $FILE {xset dpms } (GetValue 4) { } (GetValue 9) { } (GetValue 7)
               If $XSCREENSAVERCONF <> {} Then
               Begin
                  Set $RawSec = (GetValue 4)
                  SendSignal 2 1
                  Set $StandbyTime = $SecToHour {:} $SecToMin {:} $SecToSec
                  Set $RawSec = (GetValue 9)
                  SendSignal 2 1
                  Set $SuspendTime = $SecToHour {:} $SecToMin {:} $SecToSec
                  Set $RawSec = (GetValue 7)
                  SendSignal 2 1
                  Set $OffTime = $SecToHour {:} $SecToMin {:} $SecToSec

                  Set $EditXscTwoCmd = {$NSCDE_ROOT/bin/ised -c 's/^dpmsStandby:.*/dpmsStandby:	} $StandbyTime {/g' -f "} $XSCREENSAVERCONF {"}
                  Set $EditXscTwo = (GetOutput $EditXscTwoCmd 1 -1)
                  Set $EditXscThreeCmd = {$NSCDE_ROOT/bin/ised -c 's/^dpmsSuspend:.*/dpmsSuspend:	} $SuspendTime {/g' -f "} $XSCREENSAVERCONF {"}
                  Set $EditXscThree = (GetOutput $EditXscThreeCmd 1 -1)
                  Set $EditXscFourCmd = {$NSCDE_ROOT/bin/ised -c 's/^dpmsOff:.*/dpmsOff:	} $OffTime {/g' -f "} $XSCREENSAVERCONF {"}
                  Set $EditXscFour = (GetOutput $EditXscFourCmd 1 -1)
               End
            End

            Quit
         End
      End
      1 :
      Begin
         Set $SecToHourCmd = {ksh -c 'echo $((} $RawSec {/3600))'}
         Set $SecToHour = (GetOutput $SecToHourCmd 1 -1)
         Set $SecToMinCmd = {ksh -c 'echo $(((} $RawSec {%3600)/60))'}
         Set $SecToMin = (GetOutput $SecToMinCmd 1 -1)
         Set $SecToSecCmd = {ksh -c 'echo $(((} $RawSec {%60)))'}
         Set $SecToSec = (GetOutput $SecToSecCmd 1 -1)
         If (StrCopy $SecToHour 2 2) == {} Then
            Begin
               Set $SecToHour = {0} $SecToHour
            End
            If (StrCopy $SecToMin 2 2) == {} Then
            Begin
               Set $SecToMin = {0} $SecToMin
            End
            If (StrCopy $SecToSec 2 2) == {} Then
            Begin
               Set $SecToSec = {0} $SecToSec
            End
      End
End

Widget 3
   Property
   Size 72 0
   Position 204 378
   Flags NoReliefString
   Type PushButton
   Title {Dismiss}
   Main
      Case message of
      SingleClic :
      Begin
         SendSignal 3 1
      End
      1 :
      Begin
         Quit
      End
End

Widget 4
   Property
   Size 352 2
   Position 16 116
   Flags NoReliefString
   Type HScrollBar
   Value 0
   MinValue 0
   MaxValue 65535
   Main
      Case message of
      SingleClic :
      Begin
         ChangeColorset 2 20
      End
End

Widget 5
   Property
   Position 14 94
   Flags NoReliefString NoFocus Left
   Type ItemDraw
   Title { Standby time after (seconds): }
   Main
      Case message of
      SingleClic :
      Begin
      End
End

Widget 6
   Property
   Position 18 62
   Flags NoReliefString
   Type CheckBox
   Title { DPMS Enabled/Disabled}
   Value 1
   Main
      Case message of
      SingleClic :
      Begin
         If (GetValue 6) == 0 Then
         Begin
            HideWidget 4
            HideWidget 5
            HideWidget 7
            HideWidget 8
            HideWidget 9
            HideWidget 10
         End
         Else
         Begin
            ShowWidget 4
            ShowWidget 5
            ShowWidget 7
            ShowWidget 8
            ShowWidget 9
            ShowWidget 10
         End
      End
End

Widget 7
   Property
   Size 352 1
   Position 16 288
   Flags NoReliefString
   Type HScrollBar
   Value 0
   MinValue 0
   MaxValue 65535
   Main
      Case message of
      SingleClic :
      Begin
         ChangeColorset 2 20
      End
End

Widget 8
   Property
   Position 14 268
   Flags NoReliefString NoFocus
   Type ItemDraw
   Title { Off time after (seconds): }
   Main
      Case message of
      SingleClic :
      Begin
      End
End

Widget 9
   Property
   Size 352 1
   Position 16 202
   Flags NoReliefString
   Type HScrollBar
   Value 0
   MinValue 0
   MaxValue 65535
   Main
      Case message of
      SingleClic :
      Begin
         ChangeColorset 2 20
      End
End

Widget 10
   Property
   Position 14 182
   Flags NoReliefString NoFocus
   Type ItemDraw
   Title { Suspend time after (seconds): }
   Main
      Case message of
      SingleClic :
      Begin
      End
End

Widget 11
   Property
   Size 380 0
   Position 2 360
   Type Rectangle
   Main
      Case message of
      SingleClic :
      Begin
      End
End

Widget 12
   Property
   Position 12 12
   Flags NoReliefString NoFocus
   Type ItemDraw
   Title {}
   Icon NsCDE/SDtpowermgr.m.xpm
   Main
      Case message of
      SingleClic :
      Begin
      End
End

Widget 13
   Property
   Size 72 0
   Position 300 378
   Flags NoReliefString
   Type PushButton
   Title {Help}
   Main
      Case message of
      SingleClic :
      Begin
         Do {f_DisplayURL "Power Style Manager" $[NSCDE_ROOT]/share/doc/html/NsCDE-PowerSaveMgr.html}
      End
End

Widget 14
   Property
   Size 80 0
   Position 292 12
   Flags NoReliefString
   Type PushButton
   Title {Default}
   Main
      Case message of
      SingleClic :
      Begin
         ShowWidget 4
         ShowWidget 5
         ShowWidget 7
         ShowWidget 8
         ShowWidget 9
         ShowWidget 10
         ChangeValue 6 1
         ChangeValue 4 1200
         ChangeValue 9 1800
         ChangeValue 7 2400
         Do {Exec xset +dpms dpms } (GetValue 4) { } (GetValue 9) { } (GetValue 7)
         WarpPointer 2
         ChangeColorset 4 22
         ChangeColorset 9 22
         ChangeColorset 7 22
         ChangeColorset 2 20
      End
End

