WindowTitle {Occupy Workspace}
WindowSize 300 280
Colorset 22

Init
Begin
   If $WinName == {} Then
   Begin
      ChangeTitle 9 {Select Workspace and pick a Window.}
   End
   Else
   Begin
      ChangeTitle 9 $WinName
   End

   WarpPointer 2
   Key Escape A 2 1 {Quit}
   Key H A 6 1 {DisplayHelp}
   Key Return A 2 3 {Go}
   Key Down A 2 2 {Down}
   Key Up A 2 2 {Up}

   Set $SmallFont = (GetOutput {$NSCDE_ROOT/bin/getfont -v -t normal -s small} 1 -1)
   ChangeFont 3 $SmallFont
   ChangeFont 9 $SmallFont
End

Widget 1
   Property
   Size 4 20
   Position 6 10
   Type ItemDraw
   Flags NoReliefString
   Title {Window: }
   Colorset 22
   Main
      Case message of
      1 :
         Begin
            ChangeValue 2 (Add (LastString) 1)
            If $DeskNo == {} Then
            Begin
               Set $DeskNo = (Add (LastString) 1)
            End
         End
      2 :
         Begin
            ChangeTitle 2 (LastString)
         End
End

Widget 4
   Property
   Size 4 20
   Position 6 38
   Type ItemDraw
   Flags NoReliefString
   Title {Workspaces:      }
   Colorset 22
End

Widget 2
   Property
   Size 178 134
   Position 110 42
   Type List
   Flags NoReliefString
   Title {}
   Colorset 22
   Main
      Case message of
      SingleClic :
      Begin
         Set $DeskNo = (GetValue 2)
         If $WinName == {} Then
         Begin
            Do {Pick () SendToModule OccupyWorkspace SendString 9 1 $[w.id] $[w.name]}
         End
      End
      1 :
      Begin
         Set $qstr = (LastString)
         If $qstr == {Quit} Then
         Quit
      End
      2 :
      Begin
         If (LastString) == {Down} Then
         Begin
            Set $MaxElementsOut = {MaxDesk=$(echo '} (GetTitle 2) {' | tr "|" "\n" | wc -l); MaxDesk=$(($MaxDesk - 1)); echo $MaxDesk}
            Set $MaxElements = (GetOutput $MaxElementsOut 1 -1)
            If (GetValue 2) <= $MaxElements Then
            Begin
               Set $NextValue = (Add (GetValue 2) 1)
               ChangeValue 2 $NextValue
               Set $DeskNo = $NextValue
            End
            Else
            Begin
               ChangeValue 2 1
               Set $DeskNo = 1
            End
         End
         If (LastString) == {Up} Then
         Begin
               Set $PrevValue = (Add (GetValue 2) -1)
               If $PrevValue >= 1 Then
               Begin
                  ChangeValue 2 $PrevValue
                  Set $DeskNo = $PrevValue
               End
               Else
               Begin
                  Set $MaxElementsFromOne = (Add $MaxElements 1)
                  ChangeValue 2 $MaxElementsFromOne
                  Set $DeskNo = $MaxElementsFromOne
               End
         End
      End
      3 :
      Begin
         If (LastString) == {Go} Then
         Begin
            SendSignal 5 1
         End
      End
End

Widget 3
   Property
   Size 100 20
   Position 112 186
   Type CheckBox
   Value 0
   Flags NoReliefString
   Title { All Workspaces}
   Main
      Case message of
      SingleClic :
         Begin
	    If (GetValue 3) == 1 Then
	    Begin
	       If $WinName == {} Then
	          Begin
	             Do {Pick () SendToModule OccupyWorkspace SendString 9 1 $[w.id] $[w.name]}
	          End

	    Set $DeskNo = 999
	    ChangeTitle 2 { All Workspaces}
	    ChangeValue 2 1
         End
      End
End

Widget 5
   Property
   Size 80 20
   Position 10 240
   Type PushButton
   Flags NoReliefString
   Title {OK}
   Main
      Case message of
      SingleClic :
      Begin
         SendSignal 5 1
      End
      1 :
      Begin
         If $DeskNo == 999 Then
            Begin
               Do {WindowId } $WinId { Stick}
            End
         Else
            Begin
               Do {f_OccupyDesk } $WinId { 0 } (Add $DeskNo -1)
            End
         Quit
      End
End

Widget 6
   Property
   Size 80 20
   Position 208 240
   Type PushButton
   Title {Help}
   Flags NoReliefString
   Main
     Case message of
     SingleClic :
     Begin
        Do {f_DisplayURL "Occupy Workspace" $[NSCDE_ROOT]/share/doc/html/NsCDE-OccupyWorkspace.html}
     End
     1 :
     Begin
        Do {f_DisplayURL "Occupy Workspace" $[NSCDE_ROOT]/share/doc/html/NsCDE-OccupyWorkspace.html}
     End
End

Widget 8
   Property
   Size 80 20
   Position 110 240
   Type PushButton
   Title {Dismiss}
   Flags NoReliefString
   Main
      Case message of
      SingleClic :
         Begin
            Quit
         End
End

Widget 7
   Property
   Size 344 1
   Position 2 224
   Type Rectangle
End

Widget 9
   Property
   Size 226 20
   Position 76 10
   Type ItemDraw
   Flags NoReliefString
   Flags Left
   Title {}
   Colorset 22
   Main
      Case message of
      1 :
         Begin
            Set $WinIdPlusName = (LastString)

            If $WinIdPlusName == {$[w.id] $[w.name]} Then
            Begin
               Set $WinId = {}
               Set $WinName = {}
            End
            Else
            Begin
               Set $WinIdOut = {echo "} $WinIdPlusName {"}
               Set $WinId = (GetOutput $WinIdOut 1 1)
               Set $WinNameOut = {echo "} $WinIdPlusName {" | cut -d" " -f2-32}
               Set $WinName = (GetOutput $WinNameOut 1 -1)
               ChangeTitle 9 $WinName
            End
         End
End

